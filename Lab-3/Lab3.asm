;====================================================================
; Main.asm file generated by New Project wizard
;
; Created:   Cum Mar 11 2016
; Processor: 8086
; Compiler:  MASM32
;
; Before starting simulation set Internal Memory Size 
; in the 8086 model properties to 0x10000
;====================================================================
STAK    SEGMENT PARA STACK 'STACK'
        DW 20 DUP(?)
STAK    ENDS

DATA    SEGMENT PARA 'DATA'
VERI    DB 5 DUP(0)
VERISAYIM DW 0
GONDERILEN DW 0
TOTAL DB 1
DATA    ENDS

CODE    SEGMENT PARA 'CODE'
        ASSUME CS:CODE, DS:DATA, SS:STAK

	;SERI VERI ALMA KESME PROGRAMI
NEWINT  PROC FAR
	 IN AL ,7CH ;GELEN VERIYI OKU 
	 SHR AL,1
	 INC AL ;ASCII BIR FAZLASINI SAKLA
	 
	 MOV SI,VERISAYIM
	 MOV VERI[SI],AL ;DEGERI DIZIDE SAKLA
	 INC SI
	 
	 CMP SI,5 ;5 KARAKTER OLUP OLMADIGININ KONTROLU
	 JNE DEVAM
	 XOR SI,SI ;ALMA INDISINI SIFIRLA
	 MOV TOTAL,0
	 MOV AL,VERI[0]
	 OUT 7CH,AL ;SAKLANAN ILK KARAKTERI GONDER
	 INC GONDERILEN
DEVAM :  MOV VERISAYIM,SI ;VERI ALMAYA DEVAM ET
	 IRET
        
NEWINT  ENDP

;SERI VERI GONDERME KESME PROGRAMI 
NEWINT2 PROC FAR
	CMP TOTAL,1 ;5 KARAKTERIN GONDERIMI BITTI MI 
	JE DEVAM2
	MOV DI,GONDERILEN
	MOV AL,VERI[DI] ;SIRADAKI KARAKTERI GONDER
	OUT 7CH,AL
	INC DI ;SONRAKÄ° KARAKTERE GEC
	CMP DI,5
	JNE DEVAM2
	MOV TOTAL,1 ;5 KARAKTER DE GONDERILDI 
	XOR DI,DI
DEVAM2:MOV GONDERILEN,DI
	IRET
NEWINT2 ENDP	
	
START PROC FAR
        MOV AX, DATA
	MOV DS, AX
	
	;78H KESME VEKTOR NUMARASININ NEWINT'E BAGLANMASI
	XOR AX,AX
	MOV ES,AX
	MOV AL,78H
	MOV AH,4
	MUL AH
	MOV BX,AX
	LEA AX,NEWINT
	MOV WORD PTR ES:[BX],AX
	MOV AX,CS
	MOV WORD PTR ES:[BX+2],AX
	
	;79H KESME VEKTOR NUMARASININ NEWINT2 'YE BAGLANMASI
	MOV AL,79H
	MOV AH,4
	MUL AH
	MOV BX,AX
	LEA AX ,NEWINT2       
	MOV WORD PTR ES:[BX],AX
	MOV AX,CS
	MOV WORD PTR ES:[BX+2],AX
	
	;8251 'IN AYARLANMASI
	MOV AL,01001101B ;ASENKRON SEKILDE AYARLADIK
	OUT 7EH,AL
	MOV AL,40H ;KONTROL YAZMACI ILE 8251'I RESETLIYORUZ
	OUT 7EH,AL
	MOV AL,01001101B ;8 VERI BITI VE 1 CARPAN
	OUT 7EH,AL
	MOV AL,15H ;HEM RECEIVE HEM TRANSMIT OZELLIGI OLACAK SEKILDE AYARLIYORUZ
	OUT 7EH,AL
	
	;8259'UN AYARLANMASI
	MOV AL,13H ;KENAR TETIKLEMELI OLACAK SEKILDE AYARLADIK
	OUT 60H,AL
	MOV AL,78H ;IR0 KESME TIPI 78H
	OUT 62H,AL
	MOV AL,03H ;OTOMATIK KESME SONLANDIRMA VE 8086
	OUT 62H,AL
	STI


	
ENDLESS:

JMP ENDLESS
RET
START ENDP
	
	
CODE    ENDS
        END START